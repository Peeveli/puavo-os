#!/usr/bin/env tclsh

#
# see https://www.openldap.org/doc/admin24/access-control.html
# and the manual page slapd.access(5) for documentation
#

#
# GLOBAL VARIABLES
#

set ldapsuffix [lindex $argv 0]
set samba_domain [lindex $argv 1]

if {[llength $argv] != 2 || $ldapsuffix eq "" || $samba_domain eq ""} {
  puts stderr "usage: puavo-print-acl ldapsuffix samba_domain"
  exit 1
}

set rules [list]

#
# SUBROUTINES
#

proc attrs {args} {
  format {attrs="%s"} [join $args ","]
}

proc dn {{_scope ""} {_branch ""}} {
  global ldapsuffix
  set branch [expr { $_branch ne "" ? "${_branch}," : ""}]
  set scope  [expr { $_scope  ne "" ? ".${_scope}"  : ""}]
  format {dn%s="%s%s"} $scope $branch $ldapsuffix
}

proc systemgroup {groupname} {
  global ldapsuffix
  format {group/puavoSystemGroup/member="cn=%s,ou=System Groups,%s"} \
         $groupname $ldapsuffix
}

proc puavo_dn {name} {
  format {dn.exact="uid=%s,o=puavo"} $name
}

proc rule {rulelines} {
  global rules
  set uncommented_rulelines [regsub -all -line {#.*$} $rulelines ""]
  set subst_rulelines [uplevel 1 [list subst $uncommented_rulelines]]
  set rule_elems [split $subst_rulelines]
  set nonempty_rule_elems [lmap _ $rule_elems { expr { $_ ne "" ? $_ : [continue] } }]
  lappend rules [join $nonempty_rule_elems " "]
}

proc ldapset_with_base {setspec} {
  global ldapsuffix
  format {set="[%s]/%s"} $ldapsuffix $setspec
}

proc ldapset {setspec} {
  format {set="%s"} $setspec
}

proc devicetype_set {devtype} {
  ldapset [format {user/puavoDeviceType & [%s]} $devtype]
}

proc person_affiliation_set {affliation} {
  ldapset [format {user/puavoEduPersonAffiliation & [%s]} $affliation]
}

#
# some constants
#

set Desktops        "ou=Desktops"
set Devices         "ou=Devices,ou=Hosts"
set Domain_Admins   "cn=Domain Admins,ou=Groups"
set Domain_Users    "cn=Domain Users,ou=Groups"
set Files           "ou=Files,ou=Desktops"
set Groups          "ou=Groups"
set Hosts           "ou=Hosts"
set People          "ou=People"
set Printers        "ou=Printers"
set Schools         "ou=Schools,ou=Groups"
set Servers         "ou=Servers,ou=Hosts"
set System_Accounts "ou=System Accounts"
set System_Groups   "ou=System Groups"

set org_owners   [ldapset_with_base "owner & user"]
set schooladmins [ldapset "this & user/puavoAdminOfSchool"]
set teachers     [person_affiliation_set teacher]

# this means: admins of the same school as
# $this_thing (person, device, server) is in
set this_school_admins [ldapset "this/puavoSchool & user/puavoAdminOfSchool*"]

#
# RULES
#

# Samba-related rules

rule {
  [dn exact ou=Samba,ou=Hosts]
    [attrs entry ou objectClass]
      by [dn children $Servers] read
}

rule {
  [dn exact ou=Samba,ou=Hosts]
    [attrs children]
      by [dn children $Servers] write
}

rule {
  [dn children ou=Samba,ou=Hosts]
      by [dn children $Servers] write
}

rule {
  [dn exact sambaDomainName=${samba_domain}]
    [attrs sambaSID sambaDomainName]
      by $org_owners write
}

rule {
  [dn exact sambaDomainName=${samba_domain}]
      by $org_owners            write
      by [dn children $Servers] write
}

rule {
  [dn exact ou=Idmap]
      by [dn children $Servers] write
}

#

rule {
  [dn subtree $People]
    [attrs sambaNTPassword userPassword sambaAcctFlags]
      by [dn children $Servers] write
      by *                      none break
}

rule {
  [dn exact $Printers]
    [attrs ou entry objectClass]
      by [puavo_dn slave]            read
      by $org_owners                 read
      by [dn children $Servers]      read
      by [dn children $People]       read
      by [systemgroup printerqueues] read
}

rule {
  [dn exact $Printers] [attrs children]
      by $org_owners                 write
      by [dn children $Servers]      write
      by [puavo_dn slave]            read
      by [dn children $People]       read
      by [systemgroup printerqueues] read
}

rule {
  [dn children $Printers]
      by $org_owners                 write
      by [dn children $Servers]      write
      by [puavo_dn slave]            read
      by [dn children $People]       read
      by [systemgroup printerqueues] read
}

rule {
  [dn children $Servers]
    [attrs puavoDeviceCurrentImage puavoDeviceAvailableImage]
      by self                    write
      by $org_owners             write
      by $this_school_admins     write
      by [puavo_dn monitor]      read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [devicetype_set laptop] read
      by [systemgroup devices]   read
}

rule {
  [dn children $Devices]
    [attrs puavoDeviceHWInfo]
      by self                    write
      by [dn children $Servers]  write
      by [puavo_dn monitor]      read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by $org_owners             read
      by $this_school_admins     read
      by [systemgroup devices]   read
}

rule {
  [dn subtree]
      by [puavo_dn slave]       read
      by [dn children $Servers] read
      by *                      none break
}

rule {
  [dn exact $People]
    [attrs entry ou objectClass]
      by [puavo_dn puavo]        read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn pw-mgmt]      read
      by [dn children $People]   read
      by [dn subtree  $Hosts]    read
      by [systemgroup getent]    read
      by [systemgroup auth]      read
      by anonymous               auth
}

rule {
  [dn exact $People]
    [attrs children]
      by $org_owners      write
      by [puavo_dn puavo] read
}

rule {
  [dn subtree $People]
    [attrs puavoAdminOfSchool]
      by $org_owners write
      by users       read
}

rule {
  [dn subtree $People]
    filter="(puavoEduPersonAffiliation=student)"
    [attrs userPassword]
      by $org_owners   =azx
      by $teachers     =azx
      by *             none break
}

rule {
  [dn subtree $People]
    filter="(puavoLocked=TRUE)"
    [attrs userPassword]
      by self                =azx
      by $org_owners         =azx
      by $this_school_admins =azx
}

rule {
  [dn subtree $People]
    [attrs userPassword]
      by self                =azx
      by [puavo_dn pw-mgmt]  =azx
      by $org_owners         =azx
      by $this_school_admins =azx
      by anonymous           auth
}

rule {
  [dn subtree $People]
    filter="(puavoEduPersonAffiliation=student)"
    [attrs sambaPwdLastSet shadowLastChange]
      by $org_owners write
      by $teachers   write
      by *           none break
}

rule {
  [dn subtree $People]
    [attrs sambaPwdLastSet shadowLastChange]
      by self                write
      by $org_owners         write
      by $this_school_admins write
      by anonymous           auth
}

rule {
  [dn subtree $People]
    [attrs entry uid puavoId eduPersonPrincipalName objectClass \
           puavoEduPersonAffiliation puavoRemovalRequestTime]
      by $org_owners             write
      by $this_school_admins     write
      by [puavo_dn puavo]        read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn pw-mgmt]      read
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by [systemgroup getent]    read
      by [systemgroup auth]      read
      by anonymous               auth
}

rule {
  [dn subtree $People]
    [attrs uidNumber gidNumber homeDirectory givenName sn \
           puavoPreferredDesktop loginShell]
      by $org_owners             write
      by $this_school_admins     write
      by [puavo_dn puavo]        read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn pw-mgmt]      read
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by [systemgroup getent]    read
}

rule {
  [dn subtree $People]
    [attrs puavoExternalId]
      by $org_owners               write
      by $this_school_admins       write
      by self                      read
      by $teachers                 read
      by [systemgroup addressbook] read
}

rule {
  [dn subtree $People]
    [attrs givenName sn displayName puavoEduPersonReverseDisplayName]
      by $org_owners               write
      by $this_school_admins       write
      by [puavo_dn puavo-ticket]   read  # XXX to be removed later
      by [dn children $People]     read
      by [dn subtree $Hosts]       read
      by [systemgroup getent]      read
      by [systemgroup addressbook] read
}

rule {
  [dn subtree $People]
    [attrs puavoEduPersonPersonnelNumber]
      by $org_owners               write
      by $this_school_admins       write
      by [puavo_dn puavo-ticket]   read  # XXX to be removed later
      by [systemgroup addressbook] read
}

rule {
  [dn subtree $People]
    [attrs jpegPhoto mail preferredLanguage puavoLocale telephoneNumber]
      by self                      write
      by $org_owners               write
      by $this_school_admins       write
      by [puavo_dn puavo]          read
      by [puavo_dn puavo-ticket]   read  # XXX to be removed later
      by [puavo_dn pw-mgmt]        read
      by [systemgroup addressbook] read
}

rule {
  [dn subtree $People]
    [attrs puavoAcceptedTerms]
      by self                    write
      by $org_owners             write
      by $this_school_admins     write
      by [puavo_dn puavo]        read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
}

rule {
  [dn subtree $People]
    [attrs puavoSchool puavoLocked]
      by $org_owners             write
      by $this_school_admins     write
      by self                    read
      by [puavo_dn puavo]        read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [systemgroup getent]    read
}

rule {
  [dn subtree $People]
    [attrs sambaNTPassword]
      by $org_owners         =az
      by $this_school_admins =az
}

rule {
  [dn subtree $People]
      by $org_owners         write
      by $this_school_admins write
      by [dn subtree $Hosts] read
}

rule {
  [dn subtree "ou=Kerberos Realms"]
      by [puavo_dn kadmin] write
      by [puavo_dn kdc]    read
}

rule {
  [dn exact $Hosts]
      by [puavo_dn monitor]      read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by $org_owners             read
      by [dn subtree $Hosts]     read
      by [systemgroup devices]   read
      by [systemgroup servers]   read
      by [dn children $People]   read
}

rule {
  [dn exact $Devices]
    [attrs entry ou objectClass]
      by [puavo_dn monitor]      read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by $org_owners             read
      by [systemgroup devices]   read
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by anonymous               auth
}

rule {
  [dn exact $Devices]
    [attrs children]
      by $org_owners             write
      by [puavo_dn monitor]      read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [systemgroup devices]   read
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
}

rule {
  [dn children $Devices]
    [attrs entry objectClass puavoWlanChannel puavoHostname puavoTag]
      by $org_owners             write
      by $this_school_admins     write
      by self                    read
      by [puavo_dn monitor]      read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [systemgroup devices]   read
      by [dn children $People]   read
      by anonymous               auth
}

rule {
  [dn children $Devices]
    [attrs userPassword]
      by $org_owners         write
      by $this_school_admins write
      by anonymous           auth
}

rule {
  [dn children $Devices]
    [attrs puavoDeviceCurrentImage puavoDeviceMonitorsXML \
           puavoDevicePrimaryUser puavoDeviceAvailableImage]
      by self                    write
      by $org_owners             write
      by $this_school_admins     write
      by [puavo_dn monitor]      read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [systemgroup devices]   read
}

rule {
  [dn children $Devices]
      by $org_owners             write
      by $this_school_admins     write
      by self                    read
      by [puavo_dn monitor]      read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [systemgroup devices]   read
      by anonymous               auth
}

rule {
  [dn exact $Servers]
    [attrs entry ou objectClass]
      by $org_owners             read
      by [puavo_dn monitor]      read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [systemgroup servers]   read
      by anonymous               auth
}

rule {
  [dn exact $Servers]
    [attrs children]
      by $org_owners             write
      by [puavo_dn monitor]      read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [systemgroup servers]   read
}

rule {
  [dn children $Servers]
      by $org_owners             write
      by [puavo_dn monitor]      read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [systemgroup servers]   read
      by [devicetype_set laptop] none break
      by $this_school_admins     none break
      by anonymous               auth
}

rule {
  [dn children $Servers]
    [attrs entry puavoId puavoSchool ou objectClass puavoExport puavoHostname]
      by [devicetype_set laptop] read
      by $this_school_admins     read
}

rule {
  [dn exact $Domain_Admins]
    [attrs memberUid]
      by $org_owners write
}

rule {
  [dn exact $Domain_Users]
    [attrs memberUid]
      by $org_owners write
}

rule {
  [dn exact $Domain_Admins]
      by $org_owners read
}

rule {
  [dn exact $Domain_Users]
      by $org_owners read
}

rule {
  [dn exact $Groups]
    [attrs entry ou objectClass]
      by $org_owners             read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by [systemgroup getent]    read
}

rule {
  [dn exact $Groups]
    [attrs children]
      by $org_owners             write
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by [systemgroup getent]    read
}

rule {
  [dn exact $Schools]
    [attrs entry ou objectClass]
      by $org_owners             read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by [systemgroup getent]    read
}

rule {
  [dn exact $Schools]
    [attrs children]
      by $org_owners             write
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by [systemgroup getent]    read
}

rule {
  [dn subtree $Schools]
      by $org_owners             write
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by [systemgroup getent]    read
}

rule {
  [dn subtree $Groups]
    filter=(objectClass=puavoEduGroup)
    [attrs gidNumber cn puavoId objectClass]
      by $org_owners             write
      by $this_school_admins     write
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by [systemgroup getent]    read
}

rule {
  [dn subtree $Groups]
    filter=(objectClass=puavoSchool)
    [attrs gidNumber cn puavoId objectClass]
      by $org_owners             +azrwsc
      by $schooladmins           +rwsc
      by [puavo_dn puavo]        read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by [systemgroup getent]    read break
}

rule {
  [dn subtree $Groups]
    filter=(objectClass=puavoEduGroup)
      by $org_owners           write
      by $this_school_admins   write
      by [dn children $People] read
      by [dn subtree $Hosts]   read
      by [systemgroup getent]  read
}

rule {
  [dn subtree $Groups]
    filter=(objectClass=puavoSchool)
    [attrs displayName puavoNamePrefix puavoSchoolHomePageURL description \
           telephoneNumber facsimileTelephoneNumber l street postOfficeBox \
           postalAddress postalCode st jpegPhoto preferredLanguage \
           puavoLocale puavoDeviceImage puavoAllowGuest puavoPersonalDevice \
           puavoPrinterQueue puavoWirelessPrinterQueue member memberUid \
           puavoActiveService puavoDeviceAutoPowerOffMode \
           puavoAutomaticImageUpdates puavoDeviceOffHour puavoDeviceOnHour \
           puavoExternalFeed puavoMountpoint puavoTag puavoWlanChannel \
           puavoWlanSSID]
      by $org_owners             write
      by $schooladmins           write
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by [systemgroup getent]    read break
}

rule {
  [dn subtree $Groups]
    filter=(objectClass=puavoSchool)
      by $org_owners             write
      by $schooladmins           +rscxd
      by [puavo_dn puavo]        read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by [systemgroup getent]    read
}

rule {
  [dn exact $Desktops]
    [attrs entry ou objectClass]
      by $org_owners         read
      by [dn subtree $Hosts] read
}

rule {
  [dn exact $Desktops]
    [attrs children]
      by [dn children $People] read
      by [dn subtree $Hosts]   read
}

rule {
  [dn exact $Files]
    [attrs entry ou objectClass]
      by $org_owners         read
      by [dn subtree $Hosts] read
}

rule {
  [dn exact $Files]
    [attrs children]
      by $org_owners         write
      by [dn subtree $Hosts] read
}

rule {
  [dn children $Files]
      by $org_owners         write
      by [dn subtree $Hosts] read
}

rule {
  [dn subtree $System_Accounts]
    [attrs userPassword]
      by $org_owners         write
      by $this_school_admins write
      by anonymous           auth
}

rule {
  [dn subtree $System_Accounts]
      by $org_owners         write
      by $this_school_admins write
      by [puavo_dn puavo]    read
}

rule {
  [dn subtree $System_Groups]
    [attrs member]
      by $org_owners         write
      by $this_school_admins write
}

rule {
  [dn subtree $System_Groups]
      by $org_owners         read
      by $this_school_admins read
}

rule {
  [dn]
    [attrs entry]
      by [puavo_dn kadmin]       read
      by [puavo_dn kdc]          read
      by [puavo_dn monitor]      read
      by [puavo_dn puavo]        read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by $org_owners             read
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by [systemgroup getent]    read
      by [systemgroup orginfo]   read
      by *                       +sxd
}

rule {
  [dn]
    [attrs objectClass]
      by [puavo_dn kadmin]       read
      by [puavo_dn kdc]          read
      by [puavo_dn monitor]      read
      by [puavo_dn puavo]        read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by $org_owners             read
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by [systemgroup getent]    read
      by [systemgroup orginfo]   read
      by *                       +sxd
}

rule {
  [dn]
    [attrs puavoEduOrgAbbreviation st l street postalCode postalAddress \
           telephoneNumber postOfficeBox facsimileTelephoneNumber description \
           preferredLanguage puavoLocale puavoTimezone puavoKeyboardLayout \
           puavoKeyboardVariant eduOrgLegalName o owner eduOrgHomePageURI \
           puavoWlanSSID puavoWlanChannel puavoDeviceImage puavoAllowGuest \
           puavoPersonalDevice puavoActiveService puavoDeviceOnHour \
           puavoDeviceOffHour puavoDeviceAutoPowerOffMode \
           puavoImageSeriesSourceURL puavoAutomaticImageUpdates puavoConf]
      by $org_owners             write
      by [puavo_dn kadmin]       read
      by [puavo_dn kdc]          read
      by [puavo_dn monitor]      read
      by [puavo_dn puavo]        read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [dn children $People]   read
      by [dn subtree $Hosts]     read
      by [systemgroup getent]    read
      by [systemgroup orginfo]   read
      by * +sxd
}

rule {
  [dn]
    [attrs owner puavoDomain puavoPuppetHost]
      by [puavo_dn monitor]      read
      by [puavo_dn puavo]        read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [systemgroup orginfo]   read
      by $org_owners             read
}

rule {
  [dn]
      by [puavo_dn kadmin]       read
      by [puavo_dn kdc]          read
      by [puavo_dn monitor]      read
      by [puavo_dn puavo]        read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by $org_owners             read
      by *                       +sxd
}

#
# print rules
#

set rulenum 0
foreach rule $rules {
  set trimmed_rule [string trim "$rule"]
  puts "olcAccess: {${rulenum}}to ${trimmed_rule}"
  incr rulenum
}
